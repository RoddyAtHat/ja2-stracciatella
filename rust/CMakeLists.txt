cmake_minimum_required(VERSION 3.1)

project(libstracciatella)

set(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} ${CMAKE_CURRENT_SOURCE_DIR}/../cmake)

find_package(Cargo 0.9.0 REQUIRED)
find_package(Rustc 1.15.0 REQUIRED)

set(LIBSTRACCIATELLA_BUILD_SWITCHES "")
set(LIBSTRACCIATELLA_BUILD_TYPE "debug")
set(LIBSTRACCIATELLA_BUILD_DIR "${CMAKE_BINARY_DIR}/libstracciatella")
set_directory_properties(PROPERTIES ADDITIONAL_MAKE_CLEAN_FILES ${LIBSTRACCIATELLA_BUILD_DIR})

if(CMAKE_BUILD_TYPE STREQUAL "RelWithDebInfo" OR CMAKE_BUILD_TYPE STREQUAL "Release")
    set(LIBSTRACCIATELLA_BUILD_TYPE "release")
    set(LIBSTRACCIATELLA_BUILD_SWITCHES ${LIBSTRACCIATELLA_BUILD_SWITCHES} "--${LIBSTRACCIATELLA_BUILD_TYPE}")
endif()

set(LIBRARY_LOCATION "${LIBSTRACCIATELLA_BUILD_DIR}/${LIBSTRACCIATELLA_BUILD_TYPE}/")
if (MSVC)
    set(LIBRARY_LOCATION "${LIBRARY_LOCATION}deps/")
endif()
set(LIBRARY_LOCATION "${LIBRARY_LOCATION}${CMAKE_SHARED_LIBRARY_PREFIX}stracciatella${CMAKE_SHARED_LIBRARY_SUFFIX}")
set(IMPORT_LIBRARY_LOCATION "${LIBRARY_LOCATION}${CMAKE_SHARED_LIBRARY_PREFIX}stracciatella${CMAKE_IMPORT_LIBRARY_SUFFIX}")

add_custom_command(
    OUTPUT always-rebuild
    COMMAND cmake -E echo
    )

add_custom_command(
    OUTPUT ${LIBRARY_LOCATION}
    DEPENDS always-rebuild
    COMMAND ${CMAKE_COMMAND} -E env RUST_BACKTRACE=1 CARGO_TARGET_DIR=${LIBSTRACCIATELLA_BUILD_DIR} ${CARGO_EXECUTABLE} build ${LIBSTRACCIATELLA_BUILD_SWITCHES}
    WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
)

add_custom_target(
    libstracciatella
    ALL
    DEPENDS ${LIBRARY_LOCATION}
)

install(FILES ${LIBRARY_LOCATION} DESTINATION .)
install(FILES ${IMPORT_LIBRARY_LOCATION} DESTINATION . OPTIONAL)
